// Shared Gradle configuration for the Sponge projects

apply plugin: 'java'
// Plugins for IDE project generation
apply plugin: 'eclipse'
apply plugin: 'idea'

apply plugin: 'org.spongepowered.gradle'

defaultTasks 'licenseFormat', 'build'

group = 'org.spongepowered'
archivesBaseName = project.name.toLowerCase()

afterEvaluate {
    // Java version
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

// Testing dependencies
dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
}

// Source compiler configuration
tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path', '-parameters']
    options.deprecation = true
    options.encoding = 'UTF-8'
}

test {
    testLogging {
        exceptionFormat = 'full' // Always print full stack trace if something goes wrong in the unit tests
        showStandardStreams = true
    }
}

// Set manifest entries
jar {
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Created-By': "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",

                'Implementation-Title': project.name,
                'Implementation-Vendor': project.url
        )
    }
}

// The version can change until we're done configuring the project, so delay setting the manifest entries until that is final
afterEvaluate {
    jar.manifest {
        attributes['Implementation-Version'] = project.version
        if (commit) attributes['Git-Commit'] = commit
        if (branch) attributes['Git-Branch'] = branch
    }

    // Fail if one file is included several times
    tasks.withType(AbstractArchiveTask) {
        if (duplicatesStrategy == DuplicatesStrategy.INCLUDE) {
            duplicatesStrategy = DuplicatesStrategy.FAIL
        }
    }
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.links(
            'http://www.slf4j.org/apidocs/',
            'https://google.github.io/guava/releases/17.0/api/docs/',
            'https://google.github.io/guice/api-docs/4.0/javadoc/',
            'https://zml2008.github.io/configurate/configurate-core/apidocs/',
            'https://zml2008.github.io/configurate/configurate-hocon/apidocs/',
            'https://flowpowered.com/math/',
            'https://flowpowered.com/noise/',
            'http://asm.ow2.org/asm50/javadoc/user/',
            'https://docs.oracle.com/javase/8/docs/api/'
    )

    // Disable the crazy super-strict doclint tool in Java 8
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Apply code style plugins
apply plugin: 'net.minecrell.licenser'
apply plugin: 'checkstyle'

// License header formatting
license {
    ext {
        name = project.name
        organization = project.organization
        url = project.url
    }

    header = api.file('HEADER.txt')
    include '**/*.java'
    newLine = false
}

processResources {
    // Include LICENSE in final JAR
    from 'LICENSE.txt'
}

// Checkstyle settings
checkstyle {
    // TODO: Verify if custom modifications for checkstyle are still necessary (import order, @return on annotations)
    toolVersion = '7.1.2'
    configFile = api.file('checkstyle.xml')
    configProperties = [
            'basedir': project.projectDir,
            'suppressions': project.file('checkstyle-suppressions.xml'),
            'severity': 'warning'
    ]
}

// Disable checkstyle by default (won't run unless 'checkstyle' is explicitly invoked)
task checkstyle(dependsOn: tasks.withType(Checkstyle))

gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
    if (!graph.allTasks.contains(tasks.checkstyle)) {
        graph.allTasks.findAll { it.name.startsWith('checkstyle') }.each { it.enabled = false }
    }
}

// Apply deployment configuration
apply from: api.file('gradle/deploy.gradle')

// Gradle version used for generating the Gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = api.gradleVersion
}
