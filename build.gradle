// Gradle plugins
buildscript {
    repositories {
        jcenter()
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        maven {
            name = 'eclipse-staging'
            url = 'https://repo.eclipse.org/content/repositories/eclipse-staging'
        }
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'org.spongepowered:event-impl-gen:1.1.0'
        classpath 'org.spongepowered:spongegradle:0.5.1'
    }
}

version = '5.0.0-SNAPSHOT'

// Include shared Gradle configuration
ext.api = project
apply from: 'gradle/sponge.gradle'

sourceSets {
    ap {
        compileClasspath += main.compileClasspath + main.output
    }
}

// Project dependencies
dependencies {
    // Logging
    compile 'org.slf4j:slf4j-api:1.7.19'

    // Dependencies provided by Minecraft
    compile 'com.google.guava:guava:17.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    // Only included in server
    compile 'com.google.code.findbugs:jsr305:1.3.9'

    // Dependency injection
    compile 'com.google.inject:guice:4.0'

    // Plugin meta
    compile 'org.spongepowered:plugin-meta:0.1.1'

    // Configuration
    compile 'ninja.leaping.configurate:configurate-hocon:3.1.1'
    compile 'ninja.leaping.configurate:configurate-gson:3.1.1'
    compile 'ninja.leaping.configurate:configurate-yaml:3.1.1'

    compile 'com.flowpowered:flow-math:1.0.1'
    compile 'com.flowpowered:flow-noise:1.0.1-SNAPSHOT'

    // Event generation
    compile 'org.ow2.asm:asm:5.0.3'
    compile 'org.spongepowered:event-gen-core:1.1.0'
}

// JAR manifest configuration
jar {
    from sourceSets.ap.output

    manifest {
        attributes('Main-Class': 'org.spongepowered.api.util.InformativeMain')
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from sourceSets.ap.allSource
}

artifacts {
    archives sourceJar
    archives javadocJar
}

apply plugin: 'event-impl-gen'

// Compile time code generator for the event factory
genEventImpl {
    includeSrc = [
            'src/main/java/org/spongepowered/api/event/achievement/',
            'src/main/java/org/spongepowered/api/event/action/',
            'src/main/java/org/spongepowered/api/event/block/',
            'src/main/java/org/spongepowered/api/event/command/',
            'src/main/java/org/spongepowered/api/event/data/',
            'src/main/java/org/spongepowered/api/event/economy/',
            'src/main/java/org/spongepowered/api/event/entity/',
            'src/main/java/org/spongepowered/api/event/game/',
            'src/main/java/org/spongepowered/api/event/item/inventory/',
            'src/main/java/org/spongepowered/api/event/message/',
            'src/main/java/org/spongepowered/api/event/network/',
            'src/main/java/org/spongepowered/api/event/server/',
            'src/main/java/org/spongepowered/api/event/service/',
            'src/main/java/org/spongepowered/api/event/statistic/',
            'src/main/java/org/spongepowered/api/event/user/',
            'src/main/java/org/spongepowered/api/event/world/',
    ]
    excludeSrc = []
    outputDir = 'src/main/java/'
    outputFactory = 'org.spongepowered.api.event.SpongeEventFactory'
    eventImplCreateMethod = 'org.spongepowered.api.event.SpongeEventFactoryUtils.createEventImpl'
    sortPriorityPrefix = 'original'
    groupingPrefixes = ['from': 'to']
    validateCode = false
}

sortClassFields {
    add 'main', 'org.spongepowered.api.data.key.Keys'
    add 'main', 'org.spongepowered.api.data.type.ArmorTypes'
    add 'main', 'org.spongepowered.api.data.type.Arts'
    add 'main', 'org.spongepowered.api.data.type.BannerPatternShapes'
    add 'main', 'org.spongepowered.api.data.type.BigMushroomTypes'
    add 'main', 'org.spongepowered.api.data.type.BodyParts'
    add 'main', 'org.spongepowered.api.data.type.BrickTypes'
    add 'main', 'org.spongepowered.api.data.type.Careers'
    add 'main', 'org.spongepowered.api.data.type.CoalTypes'
    add 'main', 'org.spongepowered.api.data.type.ComparatorTypes'
    add 'main', 'org.spongepowered.api.data.type.CookedFishes'
    add 'main', 'org.spongepowered.api.data.type.DirtTypes'
    add 'main', 'org.spongepowered.api.data.type.DisguisedBlockTypes'
    add 'main', 'org.spongepowered.api.data.type.DoublePlantTypes'
    add 'main', 'org.spongepowered.api.data.type.DyeColors'
    add 'main', 'org.spongepowered.api.data.type.Fishes'
    add 'main', 'org.spongepowered.api.data.type.GoldenApples'
    add 'main', 'org.spongepowered.api.data.type.Hinges'
    add 'main', 'org.spongepowered.api.data.type.HorseColors'
    add 'main', 'org.spongepowered.api.data.type.HorseStyles'
    add 'main', 'org.spongepowered.api.data.type.HorseVariants'
    add 'main', 'org.spongepowered.api.data.type.InstrumentTypes'
    add 'main', 'org.spongepowered.api.data.type.LogAxes'
    add 'main', 'org.spongepowered.api.data.type.NotePitches'
    add 'main', 'org.spongepowered.api.data.type.OcelotTypes'
    add 'main', 'org.spongepowered.api.data.type.PistonTypes'
    add 'main', 'org.spongepowered.api.data.type.PlantTypes'
    add 'main', 'org.spongepowered.api.data.type.PortionTypes'
    add 'main', 'org.spongepowered.api.data.type.PrismarineTypes'
    add 'main', 'org.spongepowered.api.data.type.Professions'
    add 'main', 'org.spongepowered.api.data.type.QuartzTypes'
    add 'main', 'org.spongepowered.api.data.type.RabbitTypes'
    add 'main', 'org.spongepowered.api.data.type.RailDirections'
    add 'main', 'org.spongepowered.api.data.type.SandstoneTypes'
    add 'main', 'org.spongepowered.api.data.type.SandTypes'
    add 'main', 'org.spongepowered.api.data.type.ShrubTypes'
    add 'main', 'org.spongepowered.api.data.type.SkeletonTypes'
    add 'main', 'org.spongepowered.api.data.type.SkinParts'
    add 'main', 'org.spongepowered.api.data.type.SkullTypes'
    add 'main', 'org.spongepowered.api.data.type.SlabTypes'
    add 'main', 'org.spongepowered.api.data.type.StairShapes'
    add 'main', 'org.spongepowered.api.data.type.StoneTypes'
    add 'main', 'org.spongepowered.api.data.type.ToolTypes'
    add 'main', 'org.spongepowered.api.data.type.TreeTypes'
    add 'main', 'org.spongepowered.api.data.type.WallTypes'
    add 'main', 'org.spongepowered.api.data.type.WireAttachmentTypes'
    add 'main', 'org.spongepowered.api.block.BlockTypes'
    add 'main', 'org.spongepowered.api.block.tileentity.TileEntityTypes'
    add 'main', 'org.spongepowered.api.block.trait.BooleanTraits'
    add 'main', 'org.spongepowered.api.block.trait.EnumTraits'
    add 'main', 'org.spongepowered.api.block.trait.IntegerTraits'
    add 'main', 'org.spongepowered.api.block.trait.EnumTraits'
    add 'main', 'org.spongepowered.api.data.persistence.DataFormats'
    add 'main', 'org.spongepowered.api.effect.particle.ParticleTypes'
    add 'main', 'org.spongepowered.api.effect.potion.PotionEffectTypes'
    add 'main', 'org.spongepowered.api.effect.sound.SoundTypes'
    add 'main', 'org.spongepowered.api.entity.ai.task.AITaskTypes'
    add 'main', 'org.spongepowered.api.entity.ai.GoalTypes'
    add 'main', 'org.spongepowered.api.entity.EntityTypes'
    add 'main', 'org.spongepowered.api.entity.living.player.gamemode.GameModes'
    add 'main', 'org.spongepowered.api.event.cause.entity.damage.DamageModifierTypes'
    add 'main', 'org.spongepowered.api.event.cause.entity.damage.DamageTypes'
    add 'main', 'org.spongepowered.api.event.cause.entity.damage.source.DamageSources'
    add 'main', 'org.spongepowered.api.event.cause.entity.dismount.DismountTypes'
    add 'main', 'org.spongepowered.api.event.cause.entity.health.HealingTypes'
    add 'main', 'org.spongepowered.api.event.cause.entity.health.HealthModifierTypes'
    add 'main', 'org.spongepowered.api.event.cause.entity.health.source.HealingSources'
    add 'main', 'org.spongepowered.api.event.cause.entity.spawn.SpawnTypes'
    add 'main', 'org.spongepowered.api.event.cause.entity.teleport.TeleportTypes'
    add 'main', 'org.spongepowered.api.extra.fluid.FluidTypes'
    add 'main', 'org.spongepowered.api.item.Enchantments'
    add 'main', 'org.spongepowered.api.item.FireworkShapes'
    add 'main', 'org.spongepowered.api.item.ItemTypes'
    add 'main', 'org.spongepowered.api.item.inventory.equipment.EquipmentTypes'
    add 'main', 'org.spongepowered.api.scoreboard.Visibilities'
    add 'main', 'org.spongepowered.api.scoreboard.critieria.Criteria'
    add 'main', 'org.spongepowered.api.scoreboard.displayslot.DisplaySlots'
    add 'main', 'org.spongepowered.api.scoreboard.objective.displaymode.ObjectiveDisplayModes'
    add 'main', 'org.spongepowered.api.service.economy.transaction.TransactionTypes'
    add 'main', 'org.spongepowered.api.statistic.StatisticFormats'
    add 'main', 'org.spongepowered.api.statistic.StatisticGroups'
    add 'main', 'org.spongepowered.api.statistic.Statistics'
    add 'main', 'org.spongepowered.api.statistic.achievement.Achievements'
    add 'main', 'org.spongepowered.api.text.chat.ChatTypes'
    add 'main', 'org.spongepowered.api.text.chat.ChatVisibilities'
    add 'main', 'org.spongepowered.api.text.format.TextColors'
    add 'main', 'org.spongepowered.api.text.format.TextStyles'
    add 'main', 'org.spongepowered.api.text.selector.SelectorTypes'
    add 'main', 'org.spongepowered.api.util.ban.BanTypes'
    add 'main', 'org.spongepowered.api.util.rotation.Rotations'
    add 'main', 'org.spongepowered.api.world.biome.BiomeTypes'
    add 'main', 'org.spongepowered.api.world.DimensionTypes'
    add 'main', 'org.spongepowered.api.world.GeneratorTypes'
    add 'main', 'org.spongepowered.api.world.difficulty.Difficulties'
    add 'main', 'org.spongepowered.api.world.gen.type.BiomeTreeTypes'
    add 'main', 'org.spongepowered.api.world.gen.type.MushroomTypes'
    add 'main', 'org.spongepowered.api.world.gen.PopulatorObjects'
    add 'main', 'org.spongepowered.api.world.gen.PopulatorTypes'
    add 'main', 'org.spongepowered.api.world.gen.WorldGeneratorModifiers'
    add 'main', 'org.spongepowered.api.world.weather.Weathers'
}

// Shaded API build (with all dependencies included)
apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    classifier = 'shaded'
    from sourceSets.ap.output
}

artifacts {
    archives shadowJar
}
